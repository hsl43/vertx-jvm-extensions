apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

def bintrayUser
def bintrayApiKey

File propertiesFile = project.rootProject.file('local.properties')

if(propertiesFile.exists()) {
  Properties properties = new Properties()
  properties.load(propertiesFile.newDataInputStream())

  bintrayUser = properties.getProperty('bintray.user')
  bintrayApiKey = properties.getProperty('bintray.key')

} else {
  bintrayUser = System.getenv('BINTRAY_USER')
  bintrayApiKey = System.getenv('BINTRAY_KEY')
}

ext.configureBintrayUploadTask = { packageName ->
  final def publicationName

  if(packageName.endsWith('vertx.core.ext')) {
    publicationName = 'Core'
  } else if(packageName.endsWith('vertx.reactivex.ext')) {
    publicationName = 'RxJava2'
  } else if(packageName.endsWith('vertx.rxjava.ext')) {
    publicationName = 'RxJava1'
  } else {
    throw new IllegalArgumentException("Cannot configure publication for unexpected package name [$packageName]")
  }
  
  bintray {
    user = bintrayUser
    key = bintrayApiKey
    publications = ["${publicationName}Publication", "${publicationName}TestHelpersPublication"]

    publish = true
    override = true

    pkg {
      repo = 'vertx-ext'
      name = "$packageName"
      userOrg = 'labs2160'
      licenses = ['Apache-2.0']
      websiteUrl = 'https://github.com/hsl43/vertx-jvm-extensions'
      issueTrackerUrl = 'https://github.com/hsl43/vertx-jvm-extensions/issues'
      vcsUrl = 'https://github.com/hsl43/vertx-jvm-extensions.git'
      publicDownloadNumbers = true

      version {
        name = "$library_version"
//        desc = 'Gradle Bintray Plugin 1.0 final'  // TODO... optional
        released = new Date()
//        vcsTag = '1.3.0'  // TODO... optional
//        attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']  // TODO... optional
      }
    }
  }

  publishing {
    publications {
      "${publicationName}Publication"(MavenPublication) {
        groupId 'com.labs2160'
        artifactId "$archivesBaseName"
        artifact jar
        version "$library_version"
      }

      "${publicationName}TestHelpersPublication"(MavenPublication) {
        groupId 'com.labs2160'
        artifactId "$archivesBaseName-test-helpers"
        artifact testHelpersJar
        version "$library_version"
      }
    }
  }
}